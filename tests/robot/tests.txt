*** Settings ***
Library    Selenium2Library
Suite Setup    Go to APINF homepage
Suite Teardown    Close All Browsers

*** Variables ***
${BROWSER}    firefox
${HOMEPAGE}    https://nightly.apinf.io/
${EXISTING-API}    Test API

*** Test Cases ***
Test Search button functionality
    Click Search button

Test searching for non-existent API
    Search for non-existent API    7328o473ygtlwebhf437

Test searching for existing API
    Search for existing API    ${EXISTING-API}

Test Catalogue option in menu on Search API page
    View Catalogue

Test viewing details of existing API
    View details of existing API    ${EXISTING-API}

Test API usage instructions div is being shown
    View API usage instructions

*** Keywords ***
Go to APINF homepage
    Open Browser    ${HOMEPAGE}    ${BROWSER}
    Maximize Browser Window
    Wait Until Page Contains    APINF - API Management Platform

Click Search button
    Click Element    xpath=//button[text()='Search']
    Wait Until Page Contains    Search APIs    

Search for non-existent API    
    [Arguments]    ${searchString}
    Input Text    xpath=//input[@id='search-text']    ${searchString}
    Wait Until Page Contains    Nothing found.

Search for existing API
    [Arguments]    ${searchString}
    Input Text    id=search-text    ${searchString}
    Page Should Contain    ${searchString}

View Catalogue
    Click Element    xpath=//span[text()='Catalogue']
    Wait Until Page Contains    Available APIs

View details of existing API
    [Arguments]    ${searchString}
    Input Text    id=search-text    ${searchString}
    Click Element    xpath=//button[text()='Search']
    Wait Until Page Contains    Search results
    Click Link    ${searchString}
    Wait Until Page Contains    ${searchString}
    Wait Until Page Contains    Status

View API usage instructions
    Click Element    xpath=//button[@data-target='#usage-instructions']
    Wait Until Page Contains Element    id=usage-instructions


